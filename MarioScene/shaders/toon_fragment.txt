#version 120
#extension GL_OES_standard_derivatives : enable

struct PointLight {
    vec3 position;
    vec3 color;
    float strength;
};

varying vec2 fragmentTexCoord;
varying vec3 fragmentPosition;
varying vec3 fragmentNormal;

uniform sampler2D imageTexture;
uniform PointLight Lights[8];
uniform vec3 cameraPosition;

vec3 calculatePointLight(PointLight light, vec3 fragPosition, vec3 fragNormal);

void main()
{
    vec4 textureColor = texture2D(imageTexture, fragmentTexCoord);
    vec3 lightingColor = vec3(0.2);

    for (int i = 0; i < 8; i++) {
        lightingColor += calculatePointLight(Lights[i], fragmentPosition, fragmentNormal);
    }

    // Toon Shading
    float intensity = dot(lightingColor, vec3(0.2126, 0.7152, 0.0722));
    if (intensity > 0.8) {
        lightingColor = vec3(1.0, 0.8, 0.6);
    } else if (intensity > 0.4) {
        lightingColor = vec3(0.6, 0.4, 0.2);
    } else {
        lightingColor = vec3(0.0, 0.0, 0.0);
    }

    vec4 finalColor = vec4(textureColor.rgb * lightingColor, textureColor.a);

    gl_FragColor = finalColor;
}

vec3 calculatePointLight(PointLight light, vec3 fragPosition, vec3 fragNormal) {
    vec3 baseTexture = texture2D(imageTexture, fragmentTexCoord).rgb;
    vec3 result = vec3(0);

    vec3 fragLight = light.position - fragPosition;
    float distance = length(fragLight);
    fragLight = normalize(fragLight);
    vec3 fragCamera = normalize(cameraPosition - fragPosition);
    vec3 halfVec = normalize(fragLight + fragCamera);

    result += light.color * light.strength * max(0.0, dot(fragNormal, fragLight)) / (distance * distance) * baseTexture;
    result += light.color * light.strength * pow(max(0.0, dot(fragNormal, halfVec)), 32) / (distance * distance);

    return result;
}
